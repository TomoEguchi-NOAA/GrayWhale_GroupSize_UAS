# A model to estimate the true group size of gray whales from visual
# observations and some UAS sightings that match visual observations.
#
# Pois_Gam_v6: Observations by visual observers and UAS are modeled with independent
#              Poisson distributions. 

model{
	
	for (i in 1:n.grp){
	
		# True group size
		GS.[i] ~ dpois(mu.GS)T(1,GS.max)   
	
		# Observed group size by visual observers
		GS.Vis[i] ~ dpois(GS.[i] * p.Vis[i])T(1,GS.max)
		
		# Observed group size by UAS
		GS.UAS[i] ~ dpois(GS.[i] * p.UAS[i] )T(GS.min[i],GS.max)   
		
		# Detection probability
		logit(p.Vis[i]) <- B0 + B1 * Dist[i] + B2 * Bft[i] + B3 * Vis[i] + B4 * GS.cat[i]  +
								Obs.RF[obs[i]]
								
		logit(p.UAS[i]) <- B0.uas + B2.uas * Bft[i] + B3.uas * Vis[i]
		
		#e.Vis[i] ~ dnorm(0, 0.01)
		log.lkhd[i] <- logdensity.pois(GS.Vis[i], GS.[i]*p.Vis[i])
	}
	
	B0 ~ dnorm(0, 0.1)
	B1 ~ dnorm(0, 0.1)
	B2 ~ dnorm(0, 0.1)
	B3 ~ dnorm(0, 0.1)
	B4 ~ dnorm(0, 0.1)

	B0.uas ~ dnorm(0, 0.1)
	B1.uas ~ dnorm(0, 0.1)
	B2.uas ~ dnorm(0, 0.1)
	B3.uas ~ dnorm(0, 0.1)
	
	mu.GS ~ dunif(1, GS.max)
	
	## Observer random effect
	for(o in 1:n.obs){
		Obs.RF[o] ~ dnorm(0, tau.Obs)
	}#o
	sigma.Obs ~ dgamma(1, 0.1)
	tau.Obs <- pow(sigma.Obs,-2)
}