# A model to estimate the true group size of gray whales from visual
# observations and some UAS sightings that match visual observations.
#
# v2: UAS group size observations are considered a Poisson random variable

model{
	
	for (i in 1:n.grp){
	
		GS.Vis[i] ~ dpois(mu.Vis[i])T(1,)
		GS.UAS[i] ~ dpois(mu.UAS)T(1,)
	
		log(mu.Vis[i]) <- B0 + B1 * GS.UAS[i] + B2 * Dist[i] + B3 * Bft[i] + B4 * Vis[i] + Obs.RF[obs[i]]
		
		log.lkhd[i] <- logdensity.pois(GS.Vis[i], mu.Vis[i])
	}
	
	B0 ~ dnorm(0, 0.01)
	B1 ~ dnorm(0, 0.01)
	B2 ~ dnorm(0, 0.01)
	B3 ~ dnorm(0, 0.01)
	B4 ~ dnorm(0, 0.01)
	mu.UAS ~ dunif(1, 10)
	
	
	## Observer random effect
	for(o in 1:n.obs){
		Obs.RF[o] ~ dnorm(0, tau.Obs)
	}#o
	sigma.Obs ~ dgamma(1, 0.1)
	tau.Obs <- pow(sigma.Obs,-2)
	
}