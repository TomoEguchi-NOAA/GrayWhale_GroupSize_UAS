# A model to estimate the true group size of gray whales from visual
# observations and some UAS sightings that match visual observations.
#
# Pois_Gam_v3: UAS group size observations are considered a Gamma random variable

model{
	
	for (i in 1:n.grp){
	
		GS.Vis[i] ~ dpois(mu.Vis[i])T(1,)   # Visually observed group size
		GS.UAS[i] ~ dgamma(alpha.UAS, beta.UAS)T(1, GS.max)   # UAS-based group size
	
		log(mu.Vis[i]) <- B0 + B1 * GS.UAS[i]  + B2 * Dist[i] + B3 * Bft[i] + B4 * Vis[i] + Obs.RF[obs[i]]
		
		log.lkhd[i] <- logdensity.pois(GS.Vis[i], mu.Vis[i])
	}
	
	B0 ~ dnorm(0, 0.1)
	B1 ~ dnorm(0, 0.1)
	B2 ~ dnorm(0, 0.1)
	B3 ~ dnorm(0, 0.1)
	B4 ~ dnorm(0, 0.1)

	alpha.UAS ~ dunif(0.01, 100)
	beta.UAS ~ dunif(0.01, 100)
	
	## Observer random effect
	for(o in 1:n.obs){
		Obs.RF[o] ~ dnorm(0, tau.Obs)
	}#o
	sigma.Obs ~ dgamma(1, 0.1)
	tau.Obs <- pow(sigma.Obs,-2)
}