# A model to estimate the true group size of gray whales from visual
# observations and some UAS sightings that match visual observations.
#
# Pois_Gam_v6: Observations by visual observers and UAS are modeled with independent
#              Poisson distributions. 

model{
	
	for (i in 1:n.grp){
	
		# True group size
		GS.[i] ~ dgamma(alpha., beta.)T(GS.min.Vis[i],GS.max)   
	
		# Observed group size by visual observers
		GS.Vis[i] ~ dpois(mu.Vis[i])T(GS.min.Vis[i], GS.max)
		mu.Vis[i] <- GS.[i] * p.Vis[i]
		
		# Observed group size by UAS with p.UAS = 1.0
		GS.UAS[i] ~ dpois(GS.[i] * p.UAS[i] )T(GS.min.UAS[i],GS.max)   
		
		# Detection probability
		logit(p.Vis[i]) <- B0 + B1 * Dist[i] + B2 * Bft[i] + B3 * Vis[i] + 
								Obs.RF[obs[i]]
								
		p.UAS[i] <- 1.0 
		#ifelse(GS.I[i] == 1, 1.0, ilogit(B0.uas + B2.uas * Bft[i] + B3.uas * Vis[i]))
		
		#e.Vis[i] ~ dnorm(0, 0.01)
		log.lkhd[i] <- logdensity.pois(GS.Vis[i], GS.[i]*p.Vis[i])
	}
	
	B0 ~ dnorm(0, 0.1)
	B1 ~ dnorm(0, 0.1)
	B2 ~ dnorm(0, 0.1)
	B3 ~ dnorm(0, 0.1)
	B4 ~ dnorm(0, 0.1)

	#B0.uas ~ dnorm(0, 0.1)
	#B1.uas ~ dnorm(0, 0.1)
	#B2.uas ~ dnorm(0, 0.1)
	#B3.uas ~ dnorm(0, 0.1)
	
	# GAM(1.5, 0.5) is mean = 3, var = 6
	# GAM(2.952, 0.760) comes from fitting the gamma to UAS group sizes
	d.alpha ~ dunif(0.01, 5)
	beta. ~ dunif(0.5, 1) #0.760 #0.5
	alpha. <- beta. + d.alpha #2.952 #1.5	
	
	## Observer random effect
	for(o in 1:n.obs){
		Obs.RF[o] ~ dnorm(0, tau.Obs)
	}#o
	sigma.Obs ~ dgamma(1, 0.1)
	tau.Obs <- pow(sigma.Obs,-2)
}