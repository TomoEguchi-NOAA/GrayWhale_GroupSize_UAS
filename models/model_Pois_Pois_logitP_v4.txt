# A model to estimate the true group size of gray whales from visual
# observations and some UAS sightings that match visual observations.
#
# Pois_Pois_v4: UAS group size observations are considered a Poisson random variable

model{
	
	for (i in 1:n.grp){
	
		GS.Vis[i] ~ dpois(GS.UAS[i] * p.Vis[i])T(GS.min.Vis[i], GS.max)   # Observed group size by visual observers
		GS.UAS[i] ~ dpois(mu.GS)T(GS.min.UAS[i], GS.max)   # Observed group size by UAS
	
		logit(p.Vis[i]) <- B0 + B1 * Dist[i] + B2 * Bft[i] + B3 * Vis[i] +
								Obs.RF[obs[i]]
		
		log.lkhd[i] <- logdensity.pois(GS.Vis[i], GS.UAS[i]*p.Vis[i])
	}
	
	B0 ~ dnorm(0, 0.1)
	B1 ~ dnorm(0, 0.1)
	B2 ~ dnorm(0, 0.1)
	B3 ~ dnorm(0, 0.1)
	mu.GS ~ dunif(1, 20)
	
	## Observer random effect
	for(o in 1:n.obs){
		Obs.RF[o] ~ dnorm(0, tau.Obs)
	}#o
	sigma.Obs ~ dgamma(1, 0.1)
	tau.Obs <- pow(sigma.Obs,-2)
	
	#mu.GS ~ dgamma(alpha.mu.GS, beta.mu.GS)
	#alpha.mu.GS ~ dunif(0, 200)
	#beta.mu.GS ~ dunif(0, 100)
}